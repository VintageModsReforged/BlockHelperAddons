buildscript {
	repositories {
		mavenCentral()
		maven { url "https://maven.fabricmc.net" }
		maven { url "https://repo.sleeping.town" } //Commented out to use the version of voldeloom in the settings.gradle includeBuild (hopefully??)
	}
	dependencies {
		classpath "agency.highlysuspect:voldeloom:2.4"
	}
}

apply plugin: "agency.highlysuspect.voldeloom"

java {
	toolchain {
		//Last version able to set a --release as low as 6.
		languageVersion = JavaLanguageVersion.of(11)
	}
}

compileJava {
	//Forge doesn't understand classes compiled to versions of the class-file format newer than Java 6's.
	options.release.set(6)
}

def forgeVersion = project.minecraft_forge
def minecraftVersion = project.minecraft
archivesBaseName = project.modid
version = project.modVersion

volde {
	runs {
		client {
			programArg "Dev"
		}
	}
}

repositories {
	maven {
		url("https://mvnrepository.com/artifact/it.unimi.dsi/fastutil")
	}
	maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	forge "net.minecraftforge:forge:${forgeVersion}:universal@zip"
	mappings "net.minecraftforge:forge:${forgeVersion}:src@zip"

	modImplementation files("libs/industrialcraft-2_1.117.385-lf.jar")
	modImplementation files("libs/compactsolars-universal-1.5.2-4.2.0.52.jar")
	modImplementation files("libs/AdvancedPowerManagement-1.2.68-IC2-1.115.jar")
	modImplementation files("libs/gregtechmod311.jar")
	modImplementation files("libs/VintageCore-1.5.2-2.0.0-dev.jar")
	modImplementation files("libs/AdvancedMachines-1.5.2-4.7.5.2g-pre-dev.jar")
	modImplementation files("libs/AdvancedSolarPanels-1.5.2-4.0.0-dev.jar")
	modImplementation files("libs/BlockHelper-1.5.x-2.0.0-pre4-dev.jar")
}

def props = ['modName', 'modid', 'modVersion', 'modDescription', 'modAuthors', 'modDeps', 'minecraft', 'url']
processResources {
	filesMatching("mcmod.info") {
		expand(project.properties.subMap(props))
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

jar {
	from('compat') {
		include 'reforged/mods/blockhelper/addons/integrations/nei/**'
	}
}